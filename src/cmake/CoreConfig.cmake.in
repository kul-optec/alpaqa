@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(Eigen3 CONFIG)
find_dependency(alpaqaDl CONFIG)
if (@ALPAQA_WITH_OPENMP@)
    find_dependency(OpenMP COMPONENTS CXX)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/alpaqaCoreTargets.cmake")

# The following features may have been enabled in the $<BUILD_INTERFACE>, but
# disabled in the $<INSTALL_INTERFACE>. If they are supported on the user's
# system, we do enable them again.
if (@ALPAQA_WITH_CXX_23@ AND "cxx_std_23" IN_LIST CMAKE_CXX_COMPILE_FEATURES)
    target_compile_features(alpaqa::alpaqa INTERFACE cxx_std_23)
endif()
if (@ALPAQA_WITH_JSON@)
    find_package(nlohmann_json QUIET)
    if (nlohmann_json_FOUND)
        target_link_libraries(alpaqa::alpaqa
            INTERFACE nlohmann_json::nlohmann_json)
    endif()
endif()
if (@ALPAQA_WITH_BLAS@)
    find_dependency(BLAS)
endif()

check_required_components(alpaqaCore)